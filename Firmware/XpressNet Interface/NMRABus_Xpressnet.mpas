program NMRABus_Xpressnet;

// ******************************************************************************
//
// * Copyright:
//     (c) Mustangpeak Software 2012.
//
//     The contents of this file are subject to the GNU GPL v3 licence/ you maynot use
//     this file except in compliance with the License. You may obtain a copy of the
//     License at http://www.gnu.org/licenses/gpl.html
//
// * Revision History:
//     2012-02-01:   Created
//
// * Description:

//
// *****************************************************************************

uses
  NMRAnetStateMachine,
  NMRAnetDefines,
  CANDefines,
  CANStorage,
  MCU_Setup_dsPIC4013,
  NMRAnetEventDefines,
  NMRAnetBufferPools,
  NMRAnetBufferPoolsDefines,
  NMRAnetNode,
  dsPIC30_Traps,
  XpressnetStateMachine,
  NodeIDs;

{$I Options.inc}
const
  STATE_GATHER_COMMANDSTATIONS       = 0;
  STATE_VERIFY_COMMANDSTATIONS       = 1;
  STATE_GATHER_PROGRAMMERS           = 2;
  STATE_VERIFY_PROGRAMMERS           = 3;
  STATE_INITIALIAZE                  = 4;
  STATE_RUN                          = 5;

var
  MainLoopState: Byte;

// *****************************************************************************
//  INTERRUPTS
// *****************************************************************************

procedure INTERRUPT_Timer_840ms(); iv IVT_ADDR_T3INTERRUPT;
begin 
  // Silicon Bug in the 4013, if Interrupt is disabled, then Interrupt 2 of higher priority is called (nested) an Address Trap can be thrown
  {$IFDEF FIX_NESTED_INTERRUPT_SILICON_BUG} asm DISI #2; end; {$ENDIF}
  T3IF_bit := 0;
  
  XpressnetStateMachine_840ms_TimeTick;
end;

procedure INTERRUPT_Timer_1ms(); iv IVT_ADDR_T2INTERRUPT;
// Called once every 100m
var
  i: Integer;
begin
  // Silicon Bug in the 4013, if Interrupt is disabled, then Interrupt 2 of higher priority is called (nested) an Address Trap can be thrown
  {$IFDEF FIX_NESTED_INTERRUPT_SILICON_BUG} asm DISI #2; end; {$ENDIF}
  T2IF_bit := 0;                                                                // Clear the Flag
  for i := 0 to Nodes.Count - 1 do
    NMRAnetStateMachine_100ms_Timer(Nodes.AllocatedList[i]);
end;

{$IFDEF XPRESSNET_UART_ENABLE}
procedure INTERRUPT_XPRESSNET_UART_RX(); iv IVT_ADDR_U2RXINTERRUPT;
begin
  // Silicon Bug in the 4013, if Interrupt is disabled, then Interrupt 2 of higher priority is called (nested) an Address Trap can be thrown
  {$IFDEF FIX_NESTED_INTERRUPT_SILICON_BUG} asm DISI #2; end; {$ENDIF}
  U2RXIF_Bit := 0;              // Clear the interrupt flag to receive the next byte

  while (URXDA_U2STA_bit = 1) and (XpressnetStateMachineInfo.RS485.XPressNetData.StateMachineIndex < STATE_RS485_FULL)  do
  begin
    XpressnetStateMachine_UART_RX_StateMachine(@XpressnetStateMachineInfo, U2RXREG);
  end
end;
{$ENDIF}

{$IFDEF PINCHANGE_INTERRUPT_ENABLE}
procedure INTERRUPT_Pin_Change_Notification(); iv IVT_ADDR_CNINTERRUPT;         // 93us when a TransmitCompleteOnCANLayerPacket called with new Tx Packet waiting
var
  Flags: Byte;
begin
  // Silicon Bug in the 4013, if Interrupt is disabled, then Interrupt 2 of higher priority is called (nested) an Address Trap can be thrown
  {$IFDEF FIX_NESTED_INTERRUPT_SILICON_BUG} asm DISI #2; end; {$ENDIF}
  CNIF_bit := 0;     // Clear Flag
  PinChangeState := PORTF;
end;
{$ENDIF}

procedure ComplexActionCallback(NodeBuffer: PNMRAnetNode; Action: PQueuedAction);
var
 NodeID: TNodeID;
begin
  case Action^.nCode of
    CRC_PRODUCER_IDENTIFIED : begin
                                case MainLoopState of
                                  STATE_VERIFY_COMMANDSTATIONS :
                                    begin
                                      if NMRAnetUtilities_EqualEventID(@Action^.DataBytes, @EVENT_LC_DISTRICT_NODE) then
                                        XpressnetStateMachineInfo.CommandStationNode.AliasID := Action^.FromAlias
                                    end;
                                  STATE_VERIFY_PROGRAMMERS     :
                                    begin
                                     if NMRAnetUtilities_EqualEventID(@Action^.DataBytes, @EVENT_LC_PROGRAMMING_NODE) then
                                       XpressnetStateMachineInfo.ProgrammerNode.AliasID := Action^.FromAlias
                                   end;
                                end
                              end;
      CRC_VERIFIED_NODE_ID : begin
                                case MainLoopState of
                                  STATE_GATHER_PROGRAMMERS :
                                    begin
                                      if Action^.FromAlias = XpressnetStateMachineInfo.CommandStationNode.AliasID then
                                      begin
                                        NMRAnetUtilities_PacketBytesToNodeID(@Action^.DataBytes, NodeID, 0);
                                        XpressnetStateMachineInfo.CommandStationNode.ID[0] := NodeID[0];
                                        XpressnetStateMachineInfo.CommandStationNode.ID[1] := NodeID[1];
                                        XpressnetStateMachineInfo.CommandStationNode.State := XpressnetStateMachineInfo.CommandStationNode.State or NIDS_VERIFIED;
                                      end
                                    end;
                                  STATE_INITIALIAZE :
                                    begin
                                      if Action^.FromAlias = XpressnetStateMachineInfo.ProgrammerNode.AliasID then
                                      begin
                                        NMRAnetUtilities_PacketBytesToNodeID(@Action^.DataBytes, NodeID, 0);
                                        XpressnetStateMachineInfo.ProgrammerNode.ID[0] := NodeID[0];
                                        XpressnetStateMachineInfo.ProgrammerNode.ID[1] := NodeID[1];
                                        XpressnetStateMachineInfo.ProgrammerNode.State := XpressnetStateMachineInfo.ProgrammerNode.State or NIDS_VERIFIED;
                                      end
                                    end;
                                end
                             end;
    CRC_AMR              :   begin
                               case MainLoopState of
                                 STATE_RUN :
                                   begin
                                     NMRAnetUtilities_PacketBytesToNodeID(@Action^.DataBytes, NodeID, 0);
                                     if NMRAnetUtilities_CompareNodeIDs(NodeID, XpressnetStateMachineInfo.CommandStationNode.ID) or
                                        NMRAnetUtilities_CompareNodeIDs(NodeID, XpressnetStateMachineInfo.ProgrammerNode.ID) then
                                         MainLoopState := STATE_GATHER_COMMANDSTATIONS      // Start over we lost something
                                   end;
                               end
                             end;
    CRC_AMD              :  begin
                               case MainloopState of
                                STATE_RUN :
                                    begin
                                      // Track Node ID's in case Programmer or Command Station needed to reallocate their Alias's
                                      NMRAnetUtilities_PacketBytesToNodeID(@Action^.DataBytes, NodeID, 0);
                                      if NMRAnetUtilities_CompareNodeIDs(NodeID, XpressnetStateMachineInfo.CommandStationNode.ID) then
                                        XpressnetStateMachineInfo.CommandStationNode.AliasID := Action^.FromAlias
                                      else
                                      if NMRAnetUtilities_CompareNodeIDs(NodeID, XpressnetStateMachineInfo.ProgrammerNode.ID) then
                                        XpressnetStateMachineInfo.ProgrammerNode.AliasID := Action^.FromAlias
                                    end;
                              end
                            end;
    CRC_PC_EVENT_REPORT :   begin

                            end;
  end;
end;


// *******************
// MAIN LOOP
// *******************
var
  Buffer: TCANBuffer;
  ActiveNode: PNMRAnetNode;
begin
  CANStorage_Initialize;
  MCU_Setup_Initialize;
  NMRAnetStateMachine_Initialize(MUSTANGPEAK_ID_0_HI, MUSTANGPEAK_XPRESSNET_ID_0_LO);
  XpressnetStateMachine_Initialize;

  NMRAnetStateMachine_SetComplexCallbackFunc(@ComplexActionCallback);

  MainLoopState := 0;
  
  while (TRUE) do
    begin
      Delay_us(2000);
      
      ActiveNode := NMRAnetNode_NextNode;
    
      if ActiveNode <> PNMRAnetNode( nil) then
      begin
        NMRAnetStateMachine_Process(ActiveNode);
        case MainLoopState of
          STATE_GATHER_COMMANDSTATIONS :
            begin
               // Gather Command Stations Nodes
               if NMRAnetStateMachine_TrySendIdentifyProducer(@NMRAnetNode, @Buffer, @EVENT_LC_DISTRICT_NODE) then
                 MainLoopState := STATE_VERIFY_COMMANDSTATIONS;
             end;
          STATE_VERIFY_COMMANDSTATIONS :
            begin
               if XpressnetStateMachineInfo.CommandStationNode.AliasID <> 0 then
                 if NMRAnetStateMachine_TrySendVerifyNodeID(@NMRAnetNode, @Buffer, XpressnetStateMachineInfo.CommandStationNode.AliasID) then
                   MainLoopState := STATE_GATHER_PROGRAMMERS;
             end;
          STATE_GATHER_PROGRAMMERS :
            begin
               if XpressnetStateMachineInfo.CommandStationNode.State and NIDS_VERIFIED <> 0 then                                  // Wait for the CS to be verified
                 if NMRAnetStateMachine_TrySendIdentifyProducer(@NMRAnetNode, @Buffer, @EVENT_LC_PROGRAMMING_NODE) then   // Gather Programming Stations Nodes
                   MainLoopState := STATE_VERIFY_PROGRAMMERS;
             end;
          STATE_VERIFY_PROGRAMMERS : 
            begin
               if XpressnetStateMachineInfo.ProgrammerNode.AliasID <> 0 then
                 if NMRAnetStateMachine_TrySendVerifyNodeID(@NMRAnetNode, @Buffer, XpressnetStateMachineInfo.ProgrammerNode.AliasID) then
                   MainLoopState := STATE_INITIALIAZE;
             end;
          STATE_INITIALIAZE :
            begin
              if XpressnetStateMachineInfo.ProgrammerNode.State and NIDS_VERIFIED <> 0 then                                       // Wait for the Programmers to be verified
                MainLoopState := STATE_RUN;   // Need to send AMD to get the Node ID for the Programmer
            end;
          STATE_RUN : 
             begin
               XpressnetStateMachine_Process(@NMRAnetNode);                                            // May want this outside of Permited block, look at the statemachine
             end;
        end;
      end
    end;
end.