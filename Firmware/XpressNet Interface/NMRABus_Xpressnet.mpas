program NMRABus_Xpressnet;

// ******************************************************************************
//
// * Copyright:
//     (c) Mustangpeak Software 2012.
//
//     The contents of this file are subject to the GNU GPL v3 licence/ you maynot use
//     this file except in compliance with the License. You may obtain a copy of the
//     License at http://www.gnu.org/licenses/gpl.html
//
// * Revision History:
//     2012-02-01:   Created
//
// * Description:

//
// *****************************************************************************

uses
  NMRAnetEventDefines,
  NMRAnetStateMachine,
  MCU_Setup_dsPIC4013,
  CANDefines,
  CANStorage,
  dsPIC30_CAN,
  dsPIC30_Traps,
  XpressnetDefines,
  XpressnetStateMachine,
  NodeIDs;

{$I Options.inc}
const
  STATE_GATHER_COMMANDSTATIONS       = 0;
  STATE_VERIFY_COMMANDSTATIONS       = 1;
  STATE_GATHER_PROGRAMMERS           = 2;
  STATE_VERIFY_PROGRAMMERS           = 3;
  STATE_INITIALIAZE                  = 4;
  STATE_RUN                          = 5;

var
  MainLoopState: Byte;

// ****************************************************************************
// procedure GetUniqueIDFunc
//
//  Description:   Callback from NMRABus to load the 48 Bit Unique ID
//
// ****************************************************************************
procedure GetUniqueIDFunc(var HiID, LoID: DWord);
begin
  LoID := MUSTANGPEAK_XPRESSNET_ID_0_LO;
  HiID := MUSTANGPEAK_ID_0_HI;
end;


// *****************************************************************************
//  INTERRUPTS
// *****************************************************************************

procedure INTERRUPT_Timer_840ms(); iv IVT_ADDR_T3INTERRUPT;
begin 
  // Silicon Bug in the 4013, if Interrupt is disabled, then Interrupt 2 of higher priority is called (nested) an Address Trap can be thrown
  {$IFDEF FIX_NESTED_INTERRUPT_SILICON_BUG} asm DISI #2; end; {$ENDIF}
  T3IF_bit := 0;
  
  XpressnetStateMachine_840ms_TimeTick;
end;

procedure INTERRUPT_Timer_1ms(); iv IVT_ADDR_T2INTERRUPT;
// Called once every 1m
begin
  // Silicon Bug in the 4013, if Interrupt is disabled, then Interrupt 2 of higher priority is called (nested) an Address Trap can be thrown
  {$IFDEF FIX_NESTED_INTERRUPT_SILICON_BUG} asm DISI #2; end; {$ENDIF}
  T2IF_bit := 0;                                                                // Clear the Flag
  NMRAnetStateMachine_1ms_Timer;
end;

{$IFDEF XPRESSNET_UART_ENABLE}
procedure INTERRUPT_XPRESSNET_UART_RX(); iv IVT_ADDR_U2RXINTERRUPT;
begin
  // Silicon Bug in the 4013, if Interrupt is disabled, then Interrupt 2 of higher priority is called (nested) an Address Trap can be thrown
  {$IFDEF FIX_NESTED_INTERRUPT_SILICON_BUG} asm DISI #2; end; {$ENDIF}
  U2RXIF_Bit := 0;              // Clear the interrupt flag to receive the next byte

  while (URXDA_U2STA_bit = 1) and (XpressnetStateMachineInfo.RS485.XPressNetData.StateMachineIndex < STATE_RS485_FULL)  do
  begin
    XpressnetStateMachine_UART_RX_StateMachine(@XpressnetStateMachineInfo, U2RXREG);
  end
end;
{$ENDIF}

{$IFDEF PINCHANGE_INTERRUPT_ENABLE}
procedure INTERRUPT_Pin_Change_Notification(); iv IVT_ADDR_CNINTERRUPT;         // 93us when a TransmitCompleteOnCANLayerPacket called with new Tx Packet waiting
var
  Flags: Byte;
begin
  // Silicon Bug in the 4013, if Interrupt is disabled, then Interrupt 2 of higher priority is called (nested) an Address Trap can be thrown
  {$IFDEF FIX_NESTED_INTERRUPT_SILICON_BUG} asm DISI #2; end; {$ENDIF}
  CNIF_bit := 0;     // Clear Flag
  PinChangeState := PORTF;
end;
{$ENDIF}

procedure ComplexResponseCallback(Response: PQueuedResponse);
var
 NodeID: TNodeID;
begin
  case Response^.nCode of
    CRC_PRODUCER_IDENTIFIED : begin
                                case MainLoopState of
                                  STATE_VERIFY_COMMANDSTATIONS :
                                    begin
                                      if NMRAnetUtilities_EqualEventID(@Response^.DataBytes, @EVENT_LC_DISTRICT_NODE) then
                                        XpressnetStateMachineInfo.CommandStationNode.AliasID := Response^.Alias
                                    end;
                                  STATE_VERIFY_PROGRAMMERS     :
                                    begin
                                     if NMRAnetUtilities_EqualEventID(@Response^.DataBytes, @EVENT_LC_PROGRAMMING_NODE) then
                                       XpressnetStateMachineInfo.ProgrammerNode.AliasID := Response^.Alias
                                   end;
                                end
                              end;
      CRC_VERIFIED_NODE_ID : begin
                                case MainLoopState of
                                  STATE_GATHER_PROGRAMMERS :
                                    begin
                                      if Response^.Alias = XpressnetStateMachineInfo.CommandStationNode.AliasID then
                                      begin
                                        NMRAnetUtilities_PacketBytesToNodeID(@Response^.DataBytes, NodeID, 0);
                                        XpressnetStateMachineInfo.CommandStationNode.ID[0] := NodeID[0];
                                        XpressnetStateMachineInfo.CommandStationNode.ID[1] := NodeID[1];
                                        XpressnetStateMachineInfo.CommandStationNode.State := XpressnetStateMachineInfo.CommandStationNode.State or NIDS_VERIFIED;
                                      end
                                    end;
                                  STATE_INITIALIAZE :
                                    begin
                                      if Response^.Alias = XpressnetStateMachineInfo.ProgrammerNode.AliasID then
                                      begin
                                        NMRAnetUtilities_PacketBytesToNodeID(@Response^.DataBytes, NodeID, 0);
                                        XpressnetStateMachineInfo.ProgrammerNode.ID[0] := NodeID[0];
                                        XpressnetStateMachineInfo.ProgrammerNode.ID[1] := NodeID[1];
                                        XpressnetStateMachineInfo.ProgrammerNode.State := XpressnetStateMachineInfo.ProgrammerNode.State or NIDS_VERIFIED;
                                      end
                                    end;
                                end
                             end;
    CRC_AMR              :   begin
                               case MainLoopState of
                                 STATE_RUN :
                                   begin
                                     NMRAnetUtilities_PacketBytesToNodeID(@Response^.DataBytes, NodeID, 0);
                                     if NMRAnetUtilities_CompareNodeIDs(NodeID, XpressnetStateMachineInfo.CommandStationNode.ID) or
                                        NMRAnetUtilities_CompareNodeIDs(NodeID, XpressnetStateMachineInfo.ProgrammerNode.ID) then
                                         MainLoopState := STATE_GATHER_COMMANDSTATIONS      // Start over we lost something
                                   end;
                               end
                             end;
    CRC_AMD              :  begin
                               case MainloopState of
                                STATE_RUN :
                                    begin
                                      // Track Node ID's in case Programmer or Command Station needed to reallocate their Alias's
                                      NMRAnetUtilities_PacketBytesToNodeID(@Response^.DataBytes, NodeID, 0);
                                      if NMRAnetUtilities_CompareNodeIDs(NodeID, XpressnetStateMachineInfo.CommandStationNode.ID) then
                                        XpressnetStateMachineInfo.CommandStationNode.AliasID := Response^.Alias
                                      else
                                      if NMRAnetUtilities_CompareNodeIDs(NodeID, XpressnetStateMachineInfo.ProgrammerNode.ID) then
                                        XpressnetStateMachineInfo.ProgrammerNode.AliasID := Response^.Alias
                                    end;
                              end
                            end;
    CRC_PC_EVENT_REPORT :   begin

                            end;
  end;
end;

procedure SimpleResponseCallback(Response: Word);
begin

end;


// *******************
// MAIN LOOP
// *******************
var
  Buffer: TCANBuffer;
begin
  CANStorage_Initialize;
  MCU_Setup_Initialize;
  NMRAnetStateMachine_Initialize;
  XpressnetStateMachine_Initialize;
  
  NMRAnetStateMachine_SetComplexCallbackFunc(@ComplexResponseCallback);
  NMRAnetStateMachine_SetSimpleCallbackFunc(@SimpleResponseCallback);
  
  MainLoopState := 0;
  
  while (TRUE) do
    begin
    
      Delay_us(2000);
    
      NMRAnetStateMachine_Process;
      
      if NMRAnetCANReceive_Permited then
      begin
        case MainLoopState of
          STATE_GATHER_COMMANDSTATIONS :
            begin
               // Gather Command Stations Nodes
               if NMRAnetStateMachine_TrySendIdentifyProducer(@Buffer, NMRABus.Node.AliasID, @EVENT_LC_DISTRICT_NODE) then
                 MainLoopState := STATE_VERIFY_COMMANDSTATIONS;
             end;
          STATE_VERIFY_COMMANDSTATIONS :
            begin
               if XpressnetStateMachineInfo.CommandStationNode.AliasID <> 0 then
                 if NMRAnetStateMachine_TrySendVerifyNodeID(@Buffer, XpressnetStateMachineInfo.CommandStationNode.AliasID, NMRABus.Node.AliasID) then
                   MainLoopState := STATE_GATHER_PROGRAMMERS;
             end;
          STATE_GATHER_PROGRAMMERS :
            begin
               if XpressnetStateMachineInfo.CommandStationNode.State and NIDS_VERIFIED <> 0 then                                  // Wait for the CS to be verified
                 if NMRAnetStateMachine_TrySendIdentifyProducer(@Buffer, NMRABus.Node.AliasID, @EVENT_LC_PROGRAMMING_NODE) then   // Gather Programming Stations Nodes
                   MainLoopState := STATE_VERIFY_PROGRAMMERS;
             end;
          STATE_VERIFY_PROGRAMMERS : 
            begin
               if XpressnetStateMachineInfo.ProgrammerNode.AliasID <> 0 then
                 if NMRAnetStateMachine_TrySendVerifyNodeID(@Buffer, XpressnetStateMachineInfo.ProgrammerNode.AliasID, NMRABus.Node.AliasID) then
                   MainLoopState := STATE_INITIALIAZE;
             end;
          STATE_INITIALIAZE :
            begin
              if XpressnetStateMachineInfo.ProgrammerNode.State and NIDS_VERIFIED <> 0 then                                       // Wait for the Programmers to be verified
                MainLoopState := STATE_RUN;   // Need to send AMD to get the Node ID for the Programmer
            end;
          STATE_RUN : 
             begin
               XpressnetStateMachine_Process;                                            // May want this outside of Permited block, look at the statemachine
             end;
        end;
      end
    end;
end.