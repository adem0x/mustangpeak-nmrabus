unit NMRAnetNode;

// ******************************************************************************
//
// * Copyright:
//     (c) Mustangpeak Software 2012.
//
//     The contents of this file are subject to the GNU GPL v3 licence/ you maynot use
//     this file except in compliance with the License. You may obtain a copy of the
//     License at http://www.gnu.org/licenses/gpl.html
//
// * Revision History:
//     2012-06-15:   Created
//
// * Description:

//
// *****************************************************************************

uses
  NMRAnetNodeDefines,
  CANDefines,
  NMRAnetUtilities,
  NMRAnetBufferPoolsDefines,
  NMRAnetAppDefines,
  NMRAnetDefines,
  NMRAnetDatagramDefines;
  
  procedure NMRAnetNode_Initialize(PhysicalNodeID_HI, PhysicalNodeID_Lo: DWord);
  function NMRAnetNode_Allocate: PNMRAnetNode;
  procedure NMRAnetNode_Release(Node: PNMRAnetNode);
  function NMRAnetNode_FindByAlias(AliasID: Word): PNMRAnetNode;
  function NMRAnetNode_FindByNodeID(var NodeID: TNodeID): PNMRAnetNode;
  procedure NMRAnetNode_SetMsgFlagsOnAllNodesOnPermittedNodes(StateFlags: Word);
  function NMRAnetNode_NextNode: PNMRAnetNode;
  procedure NMRAnetNode_SortNodeList(var LocalNodes: TNodes);
  procedure NMRAnetNode_SetStateFlag(Node: PNMRAnetNode; Flag: Byte);
  procedure NMRAnetNode_ClearStateFlag(Node: PNMRAnetNode; Flag: Byte);
  function NMRAnetNode_TestStateFlag(Node: PNMRAnetNode; Flag: Byte): Boolean;
  
  procedure NMRAnetNode_SetMsgFlag(Node: PNMRAnetNode; Flag: Byte);
  procedure NMRAnetNode_ClearMsgFlag(Node: PNMRAnetNode; Flag: Byte);
  procedure NMRAnetNode_ClearMsgFlags(Node: PNMRAnetNode);
  function NMRAnetNode_TestMsgFlags(Node: PNMRAnetNode; Flag: Word; DoClear: Boolean): Boolean;
  
  
  
  // Defined in NMRAnetStateMachine.mpas
  procedure NMRAnetStateMachine_InitializeNode(NodeBuffer: PNMRAnetNode; NodeID_HI, NodeID_Lo: DWord); external;
  
  // defined in the dsPICxx_CAN_Interrupt.mpas file
  procedure LockCANInterrupt; external;
  procedure UnLockCANInterrupt; external;

var
  Nodes: TNodes;
  
implementation

// *****************************************************************************
//  procedure NMRAnetNode_SortNodeList;
//
//  Parameters:
//
//  Result:
//
//  Description:
//
//    procedure InsertionSort(a:array of char, arrlength:integer);
//    var
//      i,j,v : integer;
//    begin
//      for i := 2 to arrlength do
//      begin
//        v := a[i];
//        j := i;
//        while (j > 0) and (a[j-1] > v) do
//        begin
//          a[j] := a[j-1];
//          j := j - 1;
//        end; //while
//        a[j] := v;
//      end; //for
//    end
// *****************************************************************************
procedure NMRAnetNode_SortNodeList(var LocalNodes: TNodes);
var
  i, j: Integer;
  LocalData: PNMRAnetNode;
begin
  LockCANInterrupt;
  for i := 1 to LocalNodes.Count - 1 do
  begin
    LocalData := LocalNodes.AllocatedList[i];
    j := i;
    while (j > 0) and (LocalNodes.AllocatedList[j-1]^.Node.AliasID > LocalData^.Node.AliasID) do
    begin
      LocalNodes.AllocatedList[j] := LocalNodes.AllocatedList[j-1];
      j := j - 1;
      if j = 0 then                                                             // mPascal does not support short circuit Boolean so the [j-1] when j = 0 is bad in the while conditional define
        Break;
    end;
    LocalNodes.AllocatedList[j] := LocalData;
    
  end;
  UnLockCANInterrupt
end;

// *****************************************************************************
//  procedure BinarySearchAliasID;
//
//  Parameters:
//
//  Result:
//
//  Description:
//
// function BinarySearch(const DataSortedAscending: array of Integer; const ElementValueWanted: Integer): Integer;
// var
//   MinIndex, MaxIndex: Integer;
//   MedianIndex, MedianValue: Integer;
//  begin
//      MinIndex := Low(DataSortedAscending);
//      MaxIndex := High(DataSortedAscending);
//      while MinIndex <= MaxIndex do begin
//          MedianIndex := (MinIndex + MaxIndex) div 2; (* If you're going to change the data type here e.g. Integer to SmallInt consider the possibility of an overflow. All it needs to go bad is MinIndex=(High(MinIndex) div 2), MaxIndex = Succ(MinIndex). *)
//          MedianValue := DataSortedAscending[MedianIndex];
//          if ElementValueWanted < MedianValue then
//              MaxIndex := Pred(MedianIndex)
//          else if ElementValueWanted = MedianValue then begin
//              Result := MedianIndex;
//              Exit; (* Successful exit. *)
//          end else
//              MinIndex := Succ(MedianIndex);
//      end;
//      Result := -1; (* We couldn't find it. *)
//  end;
// *****************************************************************************
function BinarySearchAliasID(var LocalNodes: TNodes; AliasID: Word): Integer;
var
  Min, Max, Middle: Integer;
  MidAliasID: Word;
begin
  Min := 0;
  Max := LocalNodes.Count - 1;
  while Min <= Max do
  begin
    Middle := (Min + Max) shr 1;
    MidAliasID := LocalNodes.AllocatedList[Middle]^.Node.AliasID;
    if AliasID < MidAliasID then
      Max := Middle - 1
    else
    if AliasID = MidAliasID then
    begin
      Result := Middle;
      Exit
    end else
      Min := Middle + 1
  end;
  Result := -1;
end;

// *****************************************************************************
//  procedure NMRAnetlNode_FindByAlias;
//
//  Parameters:
//
//  Result:
//
//  Description:
// *****************************************************************************
function NMRAnetNode_FindByAlias(AliasID: Word): PNMRAnetNode;
var
  Index: Integer;
begin
  LockCANInterrupt;
  Result := PNMRAnetNode( nil);
  Index := BinarySearchAliasID(Nodes, AliasID);
  if (Index > -1) and (Index < Nodes.Count) then
    Result := Nodes.AllocatedList[Index];
  UnLockCANInterrupt;
end;

// *****************************************************************************
//  procedure NMRAnetNode_FindByNodeID;
//
//  Parameters:
//
//  Result:
//
//  Description:
//                READ ME:
//                   Currently this uses a dog slow search.  If we declare that
//                   the vNodes must be continous and have the same upper 3 bytes
//                   then this can collapse to a check the upper 3 bytes then check
//                   the lower 3 bytes to be between two values (NodeID and NodeID+vNodeCount)
// *****************************************************************************
function NMRAnetNode_FindByNodeID(var NodeID: TNodeID): PNMRAnetNode;
var
  i: Integer;
begin
  Result := PNMRAnetNode( nil);
  for i := 0 to Nodes.Count - 1 do
  begin
    if Nodes.AllocatedList[i]^.Node.ID[0] = NodeID[0] then
      if Nodes.AllocatedList[i]^.Node.ID[1] = NodeID[1] then
      begin
        Result := Nodes.AllocatedList[i];
        Break
      end;
  end
end;

// *****************************************************************************
//  procedure NMRAnetNode_Initialize;
//
//  Parameters:
//
//  Result:
//
//  Description:
// *****************************************************************************
procedure NMRAnetNode_Initialize(PhysicalNodeID_HI, PhysicalNodeID_Lo: DWord);
var
  i: Integer;
  Node: PNMRAnetNode;
begin
  Nodes.iActiveNode := 0;
  Nodes.Count := 0;
  for i := 0 to MAX_NODE_COUNT - 1 do
  begin
    NMRAnetStateMachine_InitializeNode(@Nodes.RawList[i], PhysicalNodeID_HI, PhysicalNodeID_LO + i);  // Physical Node + i MUST FIT IN THE LOWER 3 BYTES
    AssignConfigurationAddress(@Nodes.RawList[i], i);
    AssignRAMAddress(@Nodes.RawList[i], i);
  end;
  Node := NMRAnetNode_Allocate;                                                 // Allocate the Physical Node
  Node^.State := Node^.State and not NS_VIRTUAL;
end;

// *****************************************************************************
//  procedure NMRAnetNode_Allocate;
//
//  Parameters:
//
//  Result:
//
//  Description:  The new Node will create its alias and register to the CAN bus
//                automatically as its statemachine begins to run
// *****************************************************************************
function NMRAnetNode_Allocate: PNMRAnetNode;

var
  i: Integer;
  
begin
  LockCANInterrupt;
  Result := PNMRAnetNode( nil);
  if Nodes.Count < MAX_NODE_COUNT then
  begin
    Result := @Nodes.RawList[Nodes.Count];
    NMRAnetStateMachine_InitializeNode(Result, 0, 0);                           // The NodeID was already created in the initialization
    NMRAnetNode_SetStateFlag(Result, NS_ALLOCATED or NS_VIRTUAL);
    Nodes.AllocatedList[Nodes.Count] := Result;
    Result^.Node.AliasID := NMRAnetUtilities_CreateAliasID(Result^.Node.Seed, False); // Pregenerate it so it can be sorted
    Inc(Nodes.Count);
    NMRAnetNode_SortNodeList(Nodes);
  end;
  UnLockCANInterrupt
end;

// *****************************************************************************
//  procedure NMRAnetNode_Release;
//
//  Parameters:
//
//  Result:
//
//  Description:
// *****************************************************************************
procedure NMRAnetNode_Release(Node: PNMRAnetNode);
var
  i, j: Word;
begin
  LockCANInterrupt;
  if Node <> nil then
  begin
    if Node^.State and NS_VIRTUAL <> 0 then                                     // Only release Virtual Nodes
    begin
      i := 0;
      while i < Nodes.Count do
      begin
        if Nodes.AllocatedList[i] = Node then
        begin
          Nodes.AllocatedList[i] := PNMRAnetNode( nil);
          j := i;
          while j < Nodes.Count - 1 do
          begin
            Nodes.AllocatedList[j] := Nodes.AllocatedList[j + 1];
            Nodes.AllocatedList[j + 1] := PNMRAnetNode( nil);
            Inc(j);
          end;
          Dec(Nodes.Count);
          i := Nodes.Count;                                                     // Done, break out
        end;
        Inc(i);
      end;
      Node^.State := NS_EMPTY;                                                  // Do this last so item is not allocated in an interrupt half way through this
    end;
  end;
  UnLockCANInterrupt
end;


// *****************************************************************************
//  procedure NMRAnetNode_SetMsgFlagsOnAllNodesOnPermittedNodes;
//
//  Parameters:
//
//  Result:
//
//  Description:
// *****************************************************************************
procedure NMRAnetNode_SetMsgFlagsOnAllNodesOnPermittedNodes(MsgFlags: Word);
var
  i: Integer;
begin
  for i := 0 to Nodes.Count - 1 do
    if Nodes.AllocatedList[i]^.State and NS_PERMITTED then
      Nodes.AllocatedList[i]^.MsgFlags := Nodes.AllocatedList[i]^.MsgFlags or MsgFlags;
end;

// *****************************************************************************
//  procedure NMRAnetNode_NextNode;
//
//  Parameters:
//
//  Result:
//
//  Description:
// *****************************************************************************
function NMRAnetNode_NextNode: PNMRAnetNode;
begin
  Result := PNMRAnetNode( nil);
  LockCANInterrupt;
  if Nodes.Count > 0 then
  begin
    if Nodes.iActiveNode > Nodes.Count - 1 then
      Nodes.iActiveNode := 0;
    Result := Nodes.AllocatedList[Nodes.iActiveNode];
    Inc(Nodes.iActiveNode);
  end;
  UnLockCANInterrupt
end;

// *****************************************************************************
//  procedure NMRAnetNode_SetStateFlag;
//
//  Parameters:
//
//  Result:
//
//  Description:
// *****************************************************************************
procedure NMRAnetNode_SetStateFlag(Node: PNMRAnetNode; Flag: Byte);
begin
  LockCANInterrupt;
  Node^.State := Node^.State or Flag;
  UnLockCANInterrupt;
end;

// *****************************************************************************
//  procedure NMRAnetNode_ClearStateFlag;
//
//  Parameters:
//
//  Result:
//
//  Description:
// *****************************************************************************
procedure NMRAnetNode_ClearStateFlag(Node: PNMRAnetNode; Flag: Byte);
begin
  LockCANInterrupt;
  Node^.State := Node^.State and not Flag;
  UnLockCANInterrupt;
end;

// *****************************************************************************
//  procedure NMRAnetNode_TestStateFlag;
//
//  Parameters:
//
//  Result:
//
//  Description:
// *****************************************************************************
function NMRAnetNode_TestStateFlag(Node: PNMRAnetNode; Flag: Byte): Boolean;
begin
  LockCANInterrupt;
  Result := Node^.State and Flag <> 0;
  UnLockCANInterrupt;
end;

// *****************************************************************************
//  procedure NMRAnetNode_SetMsgFlag;
//
//  Parameters:
//
//  Result:
//
//  Description:
// *****************************************************************************
procedure NMRAnetNode_SetMsgFlag(Node: PNMRAnetNode; Flag: Byte);
begin
  LockCANInterrupt;
  Node^.MsgFlags := Node^.MsgFlags or Flag;
  UnLockCANInterrupt;
end;

// *****************************************************************************
//  procedure NMRAnetNode_ClearMsgFlag;
//
//  Parameters:
//
//  Result:
//
//  Description:
// *****************************************************************************
procedure NMRAnetNode_ClearMsgFlag(Node: PNMRAnetNode; Flag: Byte);
begin
  LockCANInterrupt;
  Node^.MsgFlags := Node^.MsgFlags and not Flag;
  UnLockCANInterrupt;
end;

// *****************************************************************************
//  procedure NMRAnetNode_ClearMsgFlags;
//
//  Parameters:
//
//  Result:
//
//  Description:
// *****************************************************************************
procedure NMRAnetNode_ClearMsgFlags(Node: PNMRAnetNode);
begin
  LockCANInterrupt;
  Node^.MsgFlags := 0;
  UnLockCANInterrupt;
end;

// *****************************************************************************
//  function NMRAnetNode_TestMsgFlags
//     Parameters:
//     Returns:
//
//     Description:
//
// *****************************************************************************
function NMRAnetNode_TestMsgFlags(Node: PNMRAnetNode; Flag: Word; DoClear: Boolean): Boolean;
begin
  LockCANInterrupt;
  Result := Node^.MsgFlags and Flag <> 0;
  if DoClear then
    NMRAnetNode_ClearMsgFlag(Node, Flag);
  UnLockCANInterrupt;
end;

end.