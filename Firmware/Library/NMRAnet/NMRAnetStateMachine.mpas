unit NMRAnetStateMachine;

// ******************************************************************************
//
// * Copyright:
//     (c) Mustangpeak Software 2012.
//
//     The contents of this file are subject to the GNU GPL v3 licence/ you maynot use
//     this file except in compliance with the License. You may obtain a copy of the
//     License at http://www.gnu.org/licenses/gpl.html
//
// * Revision History:
//     2012-02-01:   Created
//
// * Description:
//    Implements NMRAnet StateMachine for the main loop in a program.
//
// ******************************************************************************

uses
  NMRAnetEventDefines,
  NMRAnetAppDefines,
  NMRAnetDefines,
  NMRAnetUtilities,
  NMRAnetCANReceive,
  CANStorage,
  CANDefines,
  NMRAnetDatagrams,
  NMRAnetDatagramDefines,
  NMRAnetConfigurationProtocol,
  NMRAnetConfigurationProtocolDefines,
  NMRAnetAbbreviatedConfigurationProtocol,
  NMRAnetAbbreviatedConfigurationProtocolDefines;

{$I Options.inc}


  procedure NMRAnetStateMachine_Process(NodeBuffer: PNMRAnetNodeBuffer);
  procedure NMRAnetStateMachine_Disconnect(NodeBuffer: PNMRAnetNodeBuffer);
  procedure NMRAnetStateMachine_Initialize;
  procedure NMRAnetStateMachine_InitializeNodeBuffer(NodeBuffer: PNMRAnetNodeBuffer; NodeIDHi, NodeIDLo: DWord);
  procedure NMRAnetStateMachine_1ms_Timer(NodeBuffer: PNMRAnetNodeBuffer);
  
  // Receive Callback Helper Functions.  This could be done with separate callbacks for different message
  // types but that would require callback function variables that could get expensive in memory allocation so
  // here just look at the Response nCode and the CRC_xxx constants to decode what message was received for complex
  // and the SRC_xxx constants for the Simple callback
  procedure NMRAnetStateMachine_SetSimpleCallbackFunc(CallBack: PSimpleResponseCallbackFunc);
  procedure NMRAnetStateMachine_SetComplexCallbackFunc(CallBack: PComplexResponseCallbackFunc);
  
  // Send Helper Functions
  function NMRAnetStateMachine_TrySendIdentifyProducer(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer; EventID: PEventID): Boolean;
  function NMRAnetStateMachine_TrySendIdentifyConsumer(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer; EventID: PEventID): Boolean;
  function NMRAnetStateMachine_TrySendIdentifyEvents(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer): Boolean;
  function NMRAnetStateMachine_TrySendAliasMapEnquiry(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer): Boolean;
  function NMRAnetStateMachine_TrySendVerifyNodeID(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer; DestinationAliasID: Word): Boolean;
  function NMRAnetStateMachine_TrySendAbbreviatedCDI(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer; DestinationAliasID: Word): Boolean;
  
  // Exported Functions
  function TryTransmitNMRABusLayerMsg(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer; VariableField: DWord; DestinationAlias: Word; DataCount: Byte; DataBytes: PCAN_DataBytes): Boolean;

  // defined in the dsPICxx_CAN_Interrupt.mpas file
  procedure StartCANMessageEngine; external;
  procedure LockCANInterrupt; external;
  procedure UnLockCANInterrupt; external;
  
var
  TX_CANBufferList: TCANBufferList; external;
  TX_NMRAnetBufferList: TCANBufferList; external;

implementation

function NMRAnetStateMachine_TrySendAbbreviatedCDI(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer; DestinationAliasID: Word): Boolean;
var
//  BrokenBuffer: TCANBUffer;   BUG IN VERSION 5.60 with this
  DataBytes: TCAN_DataBytes;
begin
  DataBytes[0] := MTI_TYPE_SIMPLENODE_IDENTIFY_INFO_REQUEST;
  Result := TryTransmitNMRABusLayerMsg(NodeBuffer, Buffer, FT_PRODUCER_IDENDIFY, DestinationAliasID, 1, @DataBytes);
end;

function NMRAnetStateMachine_TrySendIdentifyProducer(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer; EventID: PEventID): Boolean;
//var
//  BrokenBuffer: TCANBUffer;   BUG IN VERSION 5.60 with this
begin
  Result := TryTransmitNMRABusLayerMsg(NodeBuffer, Buffer, FT_PRODUCER_IDENDIFY, 0, EVENT_ARRAY_LENGTH, PCAN_DataBytes( EventID));
end;

function NMRAnetStateMachine_TrySendIdentifyConsumer(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer; EventID: PEventID): Boolean;
//var
//  Buffer: TCANBUffer;   BUG IN VERSION 5.60 with this
begin
  Result := TryTransmitNMRABusLayerMsg(NodeBuffer, Buffer, FT_CONSUMER_IDENTIFY, 0, EVENT_ARRAY_LENGTH,  PCAN_DataBytes( EventID));
end;

function NMRAnetStateMachine_TrySendIdentifyEvents(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer): Boolean;
//var
//  Buffer: TCANBUffer;   BUG IN VERSION 5.60 with this
begin
  Result := TryTransmitNMRABusLayerMsg(NodeBuffer, Buffer, FT_EVENTS_IDENTIFY, 0, 0, nil);
end;

function NMRAnetStateMachine_TrySendAliasMapEnquiry(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer): Boolean;
begin
  Result := TryTransmitNMRABusLayerMsg(NodeBuffer, Buffer, FT_AME, 0, 0, nil);
end;

// *****************************************************************************
//  procedure NMRAnetStateMachine_TrySendVerifyNodeID
//     Parameters:
//     Returns:
//
//     Description:
//
// *****************************************************************************
function NMRAnetStateMachine_TrySendVerifyNodeID(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer; DestinationAliasID: Word): Boolean;
var
  DataBytes: TCAN_DataBytes;
begin
  if DestinationAliasID <> 0 then
  begin
    DataBytes[0] := MTI_TYPE_VERIFY_NODE_ID_NUMBER_DEST;
    Result := TryTransmitNMRABusLayerMsg(NodeBuffer, Buffer, FT_VERIFY_NODE_ID_NUMBER_DEST, DestinationAliasID, 1, @DataBytes);
  end else
    Result := TryTransmitNMRABusLayerMsg(NodeBuffer, Buffer, FT_VERIFY_NODE_ID_NUMBER, 0, 0, nil);
end;

// *****************************************************************************
//  procedure FlushResponseList
//     Parameters:
//     Returns:
//
//     Description:
//
// *****************************************************************************
procedure FlushResponses(NodeBuffer: PNMRAnetNodeBuffer);
var
  i, j: Integer;
begin
  NodeBuffer^.Responses.SimpleResponseFlags := 0;
  NodeBuffer^.Responses.iState := 0;
  NodeBuffer^.Responses.Tag := 0;
  for i := 0 to LEN_QUEUED_RESPONSE_LIST - 1 do
  begin
    NodeBuffer^.Responses.List[i].nCode := CRC_EMPTY;
    NodeBuffer^.Responses.List[i].Alias := 0;
    NodeBuffer^.Responses.List[i].DataCount := 0;
    for j := 0 to CAN_DATA_LEN - 1 do
      NodeBuffer^.Responses.List[i].DataBytes[j] := 0;
  end;
end;

// *****************************************************************************
//  procedure FlushLists
//     Parameters:
//     Returns:
//
//     Description: Clears the lists
//
// *****************************************************************************
procedure FlushLists;
begin
  CANStorage_FlushList(@TX_CANBufferList);                                      // Flush any pending packets
  CANStorage_FlushList(@TX_NMRAnetBufferList);
end;

// *****************************************************************************
//  procedure NMRAnetStateMachine_InitializeList;
//
//  Parameters: ListPtr        : Pointer to the FIFO buffer Lists
//              BufferArrayPtr : Pointer to the first item in the BufferArray
//              LasterBuffer   : Pointer to the last item in the BufferArray
//
//  Result:
//
//  Description: Initializes a FIFO Buffer Array
// *****************************************************************************
procedure NMRAnetStateMachine_InitializeList(ListPtr: PCANBufferList; BufferArrayPtr: PCANBufferArray; ListLength: Byte);
begin
  ListPtr^.Buffers := BufferArrayPtr;
  ListPtr^.Size := ListLength;
  ListPtr^.Head := @BufferArrayPtr^[0];
  ListPtr^.Tail := @BufferArrayPtr^[0];
  ListPtr^.Count := 0;
  ListPtr^.MaxCount := 0;
end;

// *****************************************************************************
//  procedure NMRAnetStateMachine_Initialize
//     Parameters:
//     Returns:
//
//     Description:
//
// *****************************************************************************
procedure NMRAnetStateMachine_SetSimpleCallbackFunc(CallBack: PSimpleResponseCallbackFunc);
begin
  SimpleResponseCallbackFunc := Callback;
end;

// *****************************************************************************
//  procedure NMRAnetStateMachine_Initialize
//     Parameters:
//     Returns:
//
//     Description:
//
// *****************************************************************************
procedure NMRAnetStateMachine_SetComplexCallbackFunc(CallBack: PComplexResponseCallbackFunc);
begin
  ComplexResponseCallbackFunc := Callback;
end;

// *****************************************************************************
//  procedure NMRAnetStateMachine_InitializeNodeBuffer
//     Parameters:
//     Returns:
//
//     Description:
//
// *****************************************************************************
procedure NMRAnetStateMachine_InitializeNodeBuffer(NodeBuffer: PNMRAnetNodeBuffer; NodeIDHi, NodeIDLo: DWord);
begin
  NodeBuffer^.DatagramReceive := PDatagramBuffer(nil);
  NodeBuffer^.DatagramTransmit := PDatagramBuffer(nil);
  NodeBuffer^.ACDI_TransmitBuffer := PACDI_TransmitBuffer(nil);
  NodeBuffer^.ACDI_ReceiveBuffer := PACDI_ReceiveBuffer(nil);
  FlushResponses(NodeBuffer);
  NodeBuffer^.State := 0;
  NodeBuffer^.iState := STATE_NMRABUS_START;
  NodeBuffer^.Responses.BufferOverrunCount := 0;
  NodeBuffer^.Node.ID[0] := NodeIDLo;
  NodeBuffer^.Node.ID[1] := NodeIDHi;
  NodeBuffer^.Node.Seed[0] := NodeIDLo;
  NodeBuffer^.Node.Seed[1] := NodeIDHi;
end;

// *****************************************************************************
//  procedure NMRAnetStateMachine_Initialize
//     Parameters:
//     Returns:
//
//     Description:
//
// *****************************************************************************
procedure NMRAnetStateMachine_Initialize;
begin
  NMRAnetCANReceive_Initialize;
  NMRAnetDatagrams_Initialize;
  NMRAnetConfigurationProtocol_Initialize;
  NMRAnetAbbreviatedConfigurationProtocol_Initialize;
  SimpleResponseCallbackFunc := PSimpleResponseCallbackFunc( nil);
  ComplexResponseCallbackFunc := PComplexResponseCallbackFunc( nil);
  NMRAnetStateMachine_InitializeList(@TX_CANBufferList, @TX_CANBuffer, TX_CAN_BUFFER_LEN);
  NMRAnetStateMachine_InitializeList(@TX_NMRAnetBufferList, @TX_NMRAnet_Buffer, TX_NMRANET_BUFFER_LEN);
end;

// *****************************************************************************
//  procedure NMRAnetStateMachine_1ms_Timer;
//     Parameters:
//     Returns:
//
//     Description:
//
// *****************************************************************************
procedure NMRAnetStateMachine_1ms_Timer(NodeBuffer: PNMRAnetNodeBuffer);
begin
  Inc(NodeBuffer^.LoginTimeCount);
  NMRAnetDatagrams_1ms_TimeTick(Nodebuffer);
  NMRAnetAbbreviatedConfigurationProtocol_1ms_TimeTick(NodeBuffer);
end;

// *****************************************************************************
//  function TestReceivedMsgFlags
//     Parameters:
//     Returns:
//
//     Description:
//
// *****************************************************************************
function TestReceivedMsgFlags(NodeBuffer: PNMRAnetNodeBuffer; Flag: Word): Boolean;
begin
  Result := NodeBuffer^.Responses.SimpleResponseFlags and Flag <> 0
end;

// *****************************************************************************
//  function ClearReceivedMsgFlags
//     Parameters:
//     Returns:
//
//     Description:
//
// *****************************************************************************
procedure ClearReceivedMsgFlags(NodeBuffer: PNMRAnetNodeBuffer; Flag: Word);
begin
  LockCANInterrupt;
  NodeBuffer^.Responses.SimpleResponseFlags := NodeBuffer^.Responses.SimpleResponseFlags and not Flag;
  UnLockCANInterrupt;
end;

// *****************************************************************************
//  function DuplicateDetected
//     Parameters:
//     Returns:
//
//     Description:
//
// *****************************************************************************
function DuplicateDetected(NodeBuffer: PNMRAnetNodeBuffer; Flag: Word; NextState: Byte): Boolean;
begin
  Result := False;
  if TestReceivedMsgFlags(NodeBuffer, Flag) then
  begin
    ClearReceivedMsgFlags(NodeBuffer, Flag);
    NodeBuffer^.iState := NextState;
    Result := True
  end
end;


// *****************************************************************************
//  procedure TryTransmitCANLayerMsg
//     Parameters:
//     Returns:
//
//     Description:
//
// *****************************************************************************
function TryTransmitCANLayerMsg(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer; VariableField: DWord; ): Boolean;
begin
  Result := False;
  if not CANStorage_ListFull(@TX_CANBufferList) then
  begin
    NMRAnetUtilities_CreateCANControlFramePacket(NodeBuffer, Buffer, VariableField);
    if CANStorage_AddToList(@TX_CANBufferList, Buffer) then
    begin
      StartCANMessageEngine();
      Result := True
    end
  end ;
end;

// *****************************************************************************
//  procedure TryTransmitNMRABusLayerMsg
//     Parameters:
//     Returns:
//
//     Description:
//
// *****************************************************************************
function TryTransmitNMRABusLayerMsg(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer; VariableField: DWord; DestinationAlias: Word; DataCount: Byte; DataBytes: PCAN_DataBytes): Boolean;
begin
  Result := False;
  if not CANStorage_ListFull(@TX_NMRAnetBufferList) then
  begin
    NMRAnetUtilities_CreateNMRABusMessagePacket(NodeBuffer, Buffer, VariableField, DestinationAlias, DataCount, DataBytes);
    if CANStorage_AddToList(@TX_NMRAnetBufferList, Buffer) then
    begin
      StartCANMessageEngine();
      Result := True;
    end
  end
end;

// *****************************************************************************
//  function NextComplexResponse
//     Parameters:
//     Returns:
//
//     Description:
//
// *****************************************************************************
function FindStoredComplexResponse(NodeBuffer: PNMRAnetNodeBuffer; nCode: Word): PQueuedResponse;
var
  i: Word;
begin
  i := 0;
  Result := nil;
  while i < LEN_QUEUED_RESPONSE_LIST do
  begin
    if NodeBuffer^.Responses.List[i].nCode = nCode then
    begin
      Result := @NodeBuffer^.Responses.List[i];
      Exit
    end;
    Inc(i);
  end;
end;

// *****************************************************************************
//  procedure HandleOptionalInteraction
//     Parameters:
//     Returns:
//
//     Description:
//
// *****************************************************************************
procedure HandleOptionalInteraction(Response: PQueuedResponse);
begin
end;

// *****************************************************************************
//  procedure SendCANLayerSimpleResponse
//
//     Parameters:  Buffer - Buffer to load to send
//                  VariableField - MIT Coding for the Message to Send
//                  Flag - Flag to test for to decide if the response is needed
//                  NextState - next state in the statemachine once the response is handled
//     Returns:
//
//     Description:
//
// *****************************************************************************
procedure SendCANLayerSimpleResponse(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer; VariableField: DWord; Flag: Word; NextState: Byte);
begin
  if TestReceivedMsgFlags(NodeBuffer, Flag) then
    if TryTransmitCANLayerMsg(NodeBuffer, Buffer, VariableField) then
      ClearReceivedMsgFlags(NodeBuffer, Flag);
  NodeBuffer^.Responses.iState := NextState
end;

// *****************************************************************************
//  procedure SendNMRAnetLayerSimpleResponse
//
//     Parameters:  Buffer - Buffer to load to send
//                  VariableField - MIT Coding for the Message to Send
//                  Flag - Flag to test for to decide if the response is needed
//                  NextState - next state in the statemachine once the response is handled
//
//     Returns:
//
//     Description:
//
// *****************************************************************************
procedure SendNMRAnetLayerSimpleResponse(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer; VariableField: DWord; Flag: Word; NextState: Byte);
begin
  if TestReceivedMsgFlags(NodeBuffer, Flag) then
    if TryTransmitNMRABusLayerMsg(NodeBuffer, Buffer, VariableField, 0, 0, nil) then
    begin
      if SimpleResponseCallbackFunc <> nil then
        SimpleResponseCallbackFunc(NodeBuffer, Flag);
      ClearReceivedMsgFlags(NodeBuffer, Flag);
    end;

  NodeBuffer^.Responses.iState := NextState
end;

// *****************************************************************************
//  procedure SendNMRALayerSimpleSendAllConsumerProducer
//
//     Parameters:  Buffer - Buffer to load to send
//                  VariableField - MIT Coding for the Message to Send
//                  nCode - The type of response needed to test for to decide if the response is needed
//                  NextState - next state in the statemachine once the response is handled
//     Returns:
//
//     Description:
//
// *****************************************************************************
procedure SendNMRALayerSimpleSendAllConsumerProducer(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer; NextState: Byte);
begin
  if TestReceivedMsgFlags(NodeBuffer, SRC_SEND_ALL_CONSUMER_PRODUCER_IDENTIFIED) then
  begin
    {$IFDEF SUPPORT_AT_LEAST_ONE_PRODUCED_EVENT}
    if NodeBuffer^.Responses.Tag < MAX_SUPPORTED_EVENTS_PRODUCED then
    begin
      if TryTransmitNMRABusLayerMsg(NodeBuffer, Buffer, FT_PRODUCER_IDENTIFIED, 0, 8, @SUPPORTED_EVENTS_PRODUCED[NodeBuffer^.Responses.Tag]) then
        Inc(NodeBuffer^.Responses.Tag);
    end else
    {$ENDIF}
    {$IFDEF SUPPORT_AT_LEAST_ONE_CONSUMED_EVENT}
    if NodeBuffer^.Responses.Tag < MAX_SUPPORTED_EVENTS then
    begin
      if TryTransmitNMRABusLayerMsg(NodeBuffer, Buffer, FT_CONSUMER_IDENTIFIED, 0, 8, @SUPPORTED_EVENTS_CONSUMED[NodeBuffer^.Responses.Tag-MAX_SUPPORTED_EVENTS_PRODUCED]) then
        Inc(NodeBuffer^.Responses.Tag);
    end else
    {$ENDIF}
    begin
      if SimpleResponseCallbackFunc <> nil then
        SimpleResponseCallbackFunc(NodeBuffer, SRC_SEND_ALL_CONSUMER_PRODUCER_IDENTIFIED);
      ClearReceivedMsgFlags(NodeBuffer, SRC_SEND_ALL_CONSUMER_PRODUCER_IDENTIFIED);              // Done
      NodeBuffer^.Responses.iState := NextState
    end
  end else
    NodeBuffer^.Responses.iState := NextState                                       // Can't skip over this one until done with all sending
end;

// *****************************************************************************
//  procedure SendNMRAnetLayerComplexResponse
//
//     Parameters:  Buffer - Buffer to load to send
//                  VariableField - MIT Coding for the Message to Send
//                  nCode - The type of response needed to test for to decide if the response is needed
//                  NextState - next state in the statemachine once the response is handled
//     Returns:
//
//     Description:
//
// *****************************************************************************
procedure SendNMRAnetLayerComplexResponse(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer; VariableField: DWord; IsAddressed: Boolean; nCode: Word; NextState: Byte);
var
  Response: PQueuedResponse;
  TargetAlias: Word;
begin
  Response := FindStoredComplexResponse(NodeBuffer, nCode);
  if Response <> nil then
  begin
    if IsAddressed then
      TargetAlias := Response^.Alias
    else
      TargetAlias := 0;
    if TryTransmitNMRABusLayerMsg(NodeBuffer, Buffer, VariableField, TargetAlias, Response^.DataCount, @Response^.DataBytes) then
    begin
      if ComplexResponseCallbackFunc <> nil then
        ComplexResponseCallbackFunc(NodeBuffer, Response);
      Response^.nCode := CRC_EMPTY;
      NodeBuffer^.Responses.iState := NextState;
    end;
  end else
    NodeBuffer^.Responses.iState := NextState
end;

// *****************************************************************************
//  procedure SendACDI_Request
//
//     Parameters:
//
//     Returns:
//
//     Description:
//
// *****************************************************************************
procedure SendACDI_Request(NodeBuffer: PNMRAnetNodeBuffer; nCode: Word; NextState: Byte);
var
  Response: PQueuedResponse;
begin
  Response := FindStoredComplexResponse(NodeBuffer, nCode);                                 // Look for Datagram Responses from Nodes this node sent a Datagram to
  if Response <> nil then
  begin
    if NMRAnetAbbreviatedConfigurationProtocol_QueueTransmit(NodeBuffer, Response^.Alias) then
      Response^.nCode := CRC_EMPTY;
  end;
  NodeBuffer^.Responses.iState := NextState
end;


// *****************************************************************************
//  procedure DispatchDatagramResponse
//
//     Parameters:
//
//     Returns:
//
//     Description:
//
// *****************************************************************************
procedure DispatchDatagramResponse(NodeBuffer: PNMRAnetNodeBuffer; nCode: Word; NextState: Byte);
var
  Response: PQueuedResponse;
  Datagram: PDatagramBuffer;
begin
  Response := FindStoredComplexResponse(NodeBuffer, nCode);                                 // Look for Datagram Responses from Nodes this node sent a Datagram to
  if Response <> nil then
  begin
    if NodeBuffer^.DatagramTransmit <> nil then
    begin                                                             // A node is responding to a Datagram this node sent
      Datagram := NodeBuffer^.DatagramTransmit;
      if Datagram^.State = DATAGRAM_BUFFER_IN_PROCESS then                     // Only look for transmit buffers that have Datagram Tranmissions in Process
        if NMRAnetUtilities_CompareAliasIDs(Datagram^.Alias, Response^.Alias) then
        begin                                                                   // The alias that sent the response (OK/Rejected) is in the list of Nodes that we sent a Datagram to!
          Datagram^.ErrorCode.MTI := Response^.DataBytes[0];                    // Copy over the results that destination node passed in the received Datagram Result message
          Datagram^.ErrorCode.SubType[0] := Response^.DataBytes[2];
          Datagram^.ErrorCode.SubType[1] := Response^.DataBytes[1];
          Datagram^.State := DATAGRAM_BUFFER_TRANSFER_COMPLETE;
          Response^.nCode := CRC_EMPTY;                                         // Free the response
          Exit;
        end;
    end;                                          // TODO THIS WILL BE RECURSIVE INTO THE LINKED LIST!!!@@@J@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
    Response^.nCode := CRC_EMPTY;                                               // The response was found but there was no matching transmiting nodes
  end;
  NodeBuffer^.Responses.iState := NextState
end;

// *****************************************************************************
//  procedure DispatchMessageResponse
//
//     Parameters:
//
//     Returns:
//
//     Description:
//
// *****************************************************************************
procedure DispatchMessageResponse(NodeBuffer: PNMRAnetNodeBuffer; nCode: Word; NextState: Byte);
var
  Response: PQueuedResponse;
begin
  Response := FindStoredComplexResponse(NodeBuffer, nCode);                                 // Look for Complex Responses from Nodes this node sent a Datagram to
  if Response <> nil then
  begin
    if ComplexResponseCallbackFunc <> nil then
      ComplexResponseCallbackFunc(NodeBuffer, Response);
    Response^.nCode := CRC_EMPTY;
  end;
  NodeBuffer^.Responses.iState := NextState
end;

// *****************************************************************************
//  procedure DispatchACDI_ReplyResponse
//
//     Parameters:
//
//     Returns:
//
//     Description:  The CAN Frames are collected in the CAN Interrupt code to
//                   make up the full ACDI message Reply that is passed here.
//
// *****************************************************************************
procedure DispatchACDI_ReplyResponse(NodeBuffer: PNMRAnetNodeBuffer; nCode: Word; NextState: Byte);
var
  Response: PQueuedResponse;
  Buffer: Word;
begin
  Response := FindStoredComplexResponse(NodeBuffer, nCode);                                 // Look for ACDI Responses from Nodes this node
  if Response <> nil then
  begin
    Buffer := Response^.DataBytes[1] shl 8;
    Buffer := (Buffer or Response^.DataBytes[0]);
    if ACDI_ReceiveCallbackFunc <> nil then
      ACDI_ReceiveCallbackFunc(NodeBuffer, PACDI_ReceiveBuffer( Buffer));  // Snuck in
    Response^.nCode := CRC_EMPTY;
  end;
  NodeBuffer^.Responses.iState := NextState
end;

// *****************************************************************************
//  procedure Responses_Process
//     Parameters:
//     Returns:
//
//     Description:
//
// *****************************************************************************
procedure Responses_Process(NodeBuffer: PNMRAnetNodeBuffer; Buffer: PCANBuffer);
const
  ADDRESSED = True;
  NONADDRESSED = False;
  
begin
  if not DuplicateDetected(NodeBuffer, SRC_DUPLICATE_NODE_ID, STATE_NMRABUS_DUPLICATE_FULL_ID) then  // Highest Priority, if detected the main loop needs to change state
    if not DuplicateDetected(NodeBuffer, SRC_DUPLICATE_ALIAS, STATE_NMRABUS_INHIBITED) then          // Next Highest Priority, if detected the main loop needs to change state
    begin
      case NodeBuffer^.Responses.iState of
        // These responses needs a response message sent and were queued up at the Interrupt level automagically
        STATE_NMRABUS_RESPONSE_SEND_RID                          : SendCANLayerSimpleResponse(NodeBuffer, Buffer, FT_RID, SRC_SEND_RID, STATE_NMRABUS_RESPONSE_SEND_AMD);
        STATE_NMRABUS_RESPONSE_SEND_AMD                          : SendCANLayerSimpleResponse(NodeBuffer, Buffer, FT_AMD, SRC_SEND_AMD, STATE_NMRABUS_RESPONSE_SEND_VERIFIED_NODE);
        STATE_NMRABUS_RESPONSE_SEND_VERIFIED_NODE                : SendNMRAnetLayerSimpleResponse(NodeBuffer, Buffer, FT_VERIFIED_NODE_ID_NUMBER, SRC_SEND_VERIFIED_NODE, STATE_NMRABUS_RESPONSE_SEND_PROTOCOL_SUPPORT);
        STATE_NMRABUS_RESPONSE_SEND_PROTOCOL_SUPPORT             : SendNMRAnetLayerComplexResponse(NodeBuffer, Buffer, FT_PROTOCOL_SUPPORT_RESPONSE, ADDRESSED, CRC_SEND_PROTOCOL_SUPPORT, STATE_NMRABUS_RESPONSE_SEND_CONSUMER_IDENTIFIED);                           // Simple Response
        STATE_NMRABUS_RESPONSE_SEND_CONSUMER_IDENTIFIED          : SendNMRAnetLayerComplexResponse(NodeBuffer, Buffer, FT_CONSUMER_IDENTIFIED, NONADDRESSED, CRC_SEND_CONSUMER_IDENTIFIED, STATE_NMRABUS_RESPONSE_SEND_PRODUCER_IDENTIFIED);
        STATE_NMRABUS_RESPONSE_SEND_PRODUCER_IDENTIFIED          : begin
                                                                     SendNMRAnetLayerComplexResponse(NodeBuffer, Buffer, FT_PRODUCER_IDENTIFIED, NONADDRESSED, CRC_SEND_PRODUCER_IDENTIFIED, STATE_NMRABUS_RESPONSE_SEND_ALL_CONSUMER_PRODUCERS);
                                                                     NodeBuffer^.Responses.Tag := 0;   // Get ready for next state
                                                                    end;
        STATE_NMRABUS_RESPONSE_SEND_ALL_CONSUMER_PRODUCERS        : SendNMRALayerSimpleSendAllConsumerProducer(NodeBuffer, Buffer, STATE_NMRABUS_RESPONSE_SEND_OPTIONAL_INTERACTION_REJECTED);
        STATE_NMRABUS_RESPONSE_SEND_OPTIONAL_INTERACTION_REJECTED : SendNMRAnetLayerComplexResponse(NodeBuffer, Buffer, FT_OPTIONAL_INTERACTION_REJECTED, ADDRESSED, CRC_SEND_OPTIONAL_INTERACTION_REJECTED, STATE_NMRABUS_ACDI_REQUEST);
        STATE_NMRABUS_ACDI_REQUEST                                : SendACDI_Request(NodeBuffer, CRC_ACDI_REQUEST, STATE_NMRABUS_DATAGRAM_RECEIVED_OK);
        
        // These responses do NOT need a predefined response message sent
        STATE_NMRABUS_DATAGRAM_RECEIVED_OK                        : DispatchDatagramResponse(NodeBuffer, CRC_DATAGRAM_RECEIVED_OK, STATE_NMRABUS_DATAGRAM_REJECTED);
        STATE_NMRABUS_DATAGRAM_REJECTED                           : DispatchDatagramResponse(NodeBuffer, CRC_DATAGRAM_REJECTED, STATE_NMRABUS_RESPONSE_CONSUMER_IDENTIFIED);
        STATE_NMRABUS_RESPONSE_CONSUMER_IDENTIFIED                : DispatchMessageResponse(NodeBuffer, CRC_CONSUMER_IDENTIFIED, STATE_NMRABUS_RESPONSE_PRODUCER_IDENTIFIED);
        STATE_NMRABUS_RESPONSE_PRODUCER_IDENTIFIED                : DispatchMessageResponse(NodeBuffer, CRC_PRODUCER_IDENTIFIED, STATE_NMRABUS_RESPONSE_AMR);
        STATE_NMRABUS_RESPONSE_AMR                                : DispatchMessageResponse(NodeBuffer, CRC_AMR, STATE_NMRABUS_RESPONSE_AMD);
        STATE_NMRABUS_RESPONSE_AMD                                : DispatchMessageResponse(NodeBuffer, CRC_AMD, STATE_NMRABUS_RESPONSE_PC_EVENT_REPORT);
        STATE_NMRABUS_RESPONSE_PC_EVENT_REPORT                    : DispatchMessageResponse(NodeBuffer, CRC_PC_EVENT_REPORT, STATE_NMRABUS_VERIFIED_NODE_ID);
        STATE_NMRABUS_VERIFIED_NODE_ID                            : DispatchMessageResponse(NodeBuffer, CRC_VERIFIED_NODE_ID, STATE_NMRABUS_ACDI_REPLY);
        STATE_NMRABUS_ACDI_REPLY                                  : DispatchACDI_ReplyResponse(NodeBuffer, CRC_ACDI_REPLY, STATE_NMRABUS_RESPONSE_SEND_RID)

      else
        NodeBuffer^.Responses.iState := STATE_NMRABUS_RESPONSE_SEND_RID;
      end;
    end
end;

// *****************************************************************************
//  procedure NMRAnetStateMachine_Process
//     Parameters:
//     Returns:
//
//     Description:
//
// *****************************************************************************

procedure NMRAnetStateMachine_Process(NodeBuffer: PNMRAnetNodeBuffer);
var
  Buffer: TCANBuffer;     
  VariableField: DWord;
begin
  case NodeBuffer^.iState of
    STATE_NMRABUS_START :
      begin
        NodeBuffer^.State := BS_INHIBITED or BS_UNITIALIZED;
        FlushLists;
        while CAN_Engine.State and CES_TRANSMITTING <> 0 do;                             // Wait for the last transmited packet
        NodeBuffer^.iState := STATE_NMRABUS_GENERATE_NODE_ALIAS;
      end;
    STATE_NMRABUS_GENERATE_NODE_ALIAS :
      begin
        NodeBuffer^.Node.AliasID := NMRAnetUtilities_CreateAliasID(NodeBuffer^.Node.Seed, False);
        NodeBuffer^.Responses.iCID := 0;
        NodeBuffer^.iState := STATE_NMRABUS_TRANSMIT_CID;
      end;
    STATE_RANDOM_NUMBER_GENERATOR :
      begin
        NMRAnetUtilities_PsudoRandomNumberGeneratorOnSeed(NodeBuffer^.Node.Seed);
        NodeBuffer^.iState := STATE_NMRABUS_GENERATE_NODE_ALIAS;
      end;
    STATE_NMRABUS_TRANSMIT_CID :
      begin
        case NodeBuffer^.Responses.iCID of
          0 : VariableField := FT_CID0;   // Queue up
          1 : VariableField := FT_CID1;
          2 : VariableField := FT_CID2;
          3 : VariableField := FT_CID3;
        end;
        if TryTransmitCANLayerMsg(NodeBuffer, @Buffer, VariableField) then
          NodeBuffer^.iState := STATE_NMRABUS_NEXT_CDI;
      end;
    STATE_NMRABUS_NEXT_CDI :
      begin
        if NodeBuffer^.Responses.iCID < 3 then
        begin
          Inc(NodeBuffer^.Responses.iCID);
          NodeBuffer^.iState := STATE_NMRABUS_TRANSMIT_CID
        end else 
        begin
          if CAN_Engine.State and CES_TRANSMITTING = 0 then     // Wait until the transmission is complete before waiting the 200ms
          begin
            NodeBuffer^.iState := STATE_NMRABUS_WAITSTATE;
            NodeBuffer^.LoginTimeCount := 0;
          end
        end
      end;
    STATE_NMRABUS_WAITSTATE :
      begin
        if NodeBuffer^.LoginTimeCount > MAX_BUS_LOGIN_TIMEOUT then
          NodeBuffer^.iState := STATE_NMRABUS_SEND_LOGIN_RID;
      end;
    STATE_NMRABUS_SEND_LOGIN_RID :
      begin
        if not DuplicateDetected(NodeBuffer, SRC_DUPLICATE_ALIAS, STATE_RANDOM_NUMBER_GENERATOR) then
          if TryTransmitCANLayerMsg(NodeBuffer, @Buffer, FT_RID) then
            NodeBuffer^.iState := STATE_NMRABUS_SEND_LOGIN_AMD;
      end;
    STATE_NMRABUS_SEND_LOGIN_AMD :
      begin
        if not DuplicateDetected(NodeBuffer, SRC_DUPLICATE_ALIAS, STATE_RANDOM_NUMBER_GENERATOR) then
          if TryTransmitCANLayerMsg(NodeBuffer, @Buffer, FT_AMD) then
          begin
            NodeBuffer^.State := NodeBuffer^.State or BS_PERMITED and not BS_INHIBITED;
            NodeBuffer^.iState := STATE_NMRABUS_INITIALIZED
          end
      end;
    STATE_NMRABUS_INITIALIZED :
      begin
        if not DuplicateDetected(NodeBuffer, SRC_DUPLICATE_ALIAS, STATE_RANDOM_NUMBER_GENERATOR) then
          if TryTransmitNMRABusLayerMsg(NodeBuffer, @Buffer, FT_INITIALIZATION_COMPLETE, 0, 0, nil) then
          begin
            NodeBuffer^.iState := STATE_NMRABUS_LOGIN_IDENTIFY_EVENTS;
            NodeBuffer^.State := NodeBuffer^.State or BS_INITIALIZED and not BS_UNITIALIZED
          end
      end;
    STATE_NMRABUS_LOGIN_IDENTIFY_EVENTS :
      begin
        LockCANInterrupt;
        NodeBuffer^.Responses.SimpleResponseFlags := NodeBuffer^.Responses.SimpleResponseFlags or SRC_SEND_ALL_CONSUMER_PRODUCER_IDENTIFIED;
        UnLockCANInterrupt;
        NodeBuffer^.iState := STATE_NMRABUS_PERMITTED;
      end;
    STATE_NMRABUS_PERMITTED :
      begin
        Responses_Process(NodeBuffer, @Buffer);                                 // Handle auto responses to CAN/NMRAnet messages coming in
        NMRAnetDatagrams_ProcessReceive(NodeBuffer);                                        // Handle Datagrams coming in
        NMRAnetDatagrams_ProcessTransmit(NodeBuffer);                           // Handle Datagrams going out
        NMRAnetConfigurationProtocol_Process(NodeBuffer);
        NMRAnetAbbreviatedConfigurationProtocol_Process(NodeBuffer);
      end;
    STATE_NMRABUS_INHIBITED :
      begin
        if TryTransmitCANLayerMsg(NodeBuffer, @Buffer, FT_AMR) then
        begin
          FlushLists;
          FlushResponses(NodeBuffer);
          NodeBuffer^.State := NodeBuffer^.State or BS_INHIBITED and not BS_PERMITED;
          NodeBuffer^.iState := STATE_RANDOM_NUMBER_GENERATOR;
        end
      end;
    STATE_NMRABUS_DUPLICATE_FULL_ID :
      begin
        if TryTransmitCANLayerMsg(NodeBuffer, @Buffer, FT_AMR) then
        begin
          FlushLists;
          FlushResponses(NodeBuffer);
          NodeBuffer^.State := NodeBuffer^.State or BS_INHIBITED and not BS_PERMITED;
          NodeBuffer^.iState := STATE_NMRABUS_TAKE_OFFLINE
        end
      end;
    STATE_NMRABUS_TAKE_OFFLINE :
      begin
    //    if TryTransmitNMRABusLayerMsg(PCER_DUPLICATE_ID..., @Buffer) then
    //    begin
    //      NMRABus.State := NMRABus.State or BS_INHIBITED and not BS_PERMITED;
          NodeBuffer^.iState := STATE_NMRABUS_OFFLINE
    //    end
      end;
    STATE_NMRABUS_OFFLINE :
      begin
        // Done until reboot
      end
  else
    NodeBuffer^.iState := STATE_NMRABUS_START;
  end
end;

// *****************************************************************************
//  procedure NMRAnetStateMachine_Disconnect
//     Parameters:
//     Returns:
//
//     Description:
//
// *****************************************************************************
procedure NMRAnetStateMachine_Disconnect(NodeBuffer: PNMRAnetNodeBuffer);
begin
  NodeBuffer^.iState := STATE_NMRABUS_INHIBITED
end;

end.